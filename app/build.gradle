plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.feilongproject.baassetsdownloader'
    compileSdk 33

    defaultConfig {
        applicationId "com.feilongproject.baassetsdownloader"
        minSdk 24
        targetSdk 33
        versionCode = getSelfDefinedVersion("code")
        versionName = getSelfDefinedVersion("name")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }


    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "BAAssetsDownloader-v${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            //启用代码缩减、混淆处理和优化
            minifyEnabled true
            //启用资源文件缩减、优化
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:1.3.3"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation 'androidx.compose.material3:material3:1.1.0-alpha05'
    implementation "androidx.compose.material3:material3-window-size-class:1.0.1"
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.29.1-alpha'
}

static def getSelfDefinedVersion(type) {
    int today = new Date().format("yyMMddHH").toInteger()
    if (type == "code") {
        return today
    } else if (type == "name") {
        Process process = "git rev-parse --short HEAD".execute()
        process.waitFor()
        String sha1 = process.getText().trim()
        return "$today.$sha1"
    }
}
